#created on: Nov 15, 2010
package com.snda.infrastructure.drools.tutorial.validation

import org.drools.runtime.rule.RuleContext;

import com.snda.infrastructure.drools.tutorial.validation.*;

global ReportFactory reportFactory;
global BankingInquiryService inquiryService;
global ValidationReport validationReport;

import function com.snda.infrastructure.drools.tutorial.validation.ValidationHelper.*;

rule "addressRequired"
	when
		Customer( address == null )
	then
		warning(kcontext);
end

rule "accountBalanceAtLeast"
	when
		$account : Account( balance < 100 )
	then
		warning(kcontext, $account);
end

rule "studentAccountCustomerAgeLessThan"
	when
		$customer : Customer( eval(yearsPassedSince(dateOfBirth) >= 27) )
		$account : Account( customer == $customer, type == Account.Type.STUDENT )
	then
		error(kcontext, $account);
end

rule "accountNumberUnique"
	when
		$account : Account( eval(!inquiryService.isAccountNumberUnique($account)) )
	then
		error(kcontext, $account);
end


